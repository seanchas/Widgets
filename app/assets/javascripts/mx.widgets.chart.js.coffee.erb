global = exports ? this

global.mx           ||= {}
global.mx.widgets   ||= {}

scope = global.mx.widgets


cs_host         = "<%= Rails.application.config.cs_host %>"
cs_extension    = "png"
cs_template     = "adv_no_volume"
cs_compare_template = "#{cs_template}_comp"


$ = jQuery


extend = (obj, mixin) ->
    obj[name] = method for name, method of mixin
    obj

calculate_dimensions = (element, options = {}) ->
    width   = options.width || element.width()
    height  = if options.proportions then width / options.proportions else options.height || width / 2

    'c.width':      width
    'z1.width':     width
    'z1_c.width':   width

    'c.height':     height
    'z1.height':    height
    'z1_c.height':  height


make_url = (element, engine, market, security, options = {}) ->
    dimensions = calculate_dimensions(element, options)

    if options.compare?
        compare = if _.isArray(options.compare) then options.compare.join(',') else options.compare
    params = extend(dimensions, { template: cs_template, _: +new Date, compare: compare ? '', compare_template: cs_compare_template, candles: '72', interval: 10 })
    params = extend(params, { previous_session: options.previous_session }) if options.previous_session
    query = $.param(params)

    if options.board_group_id?
        "#{cs_host}/engines/#{engine}/markets/#{market}/boardgroups/#{options.board_group_id}/securities/#{security}.#{cs_extension}?#{query}"
    else
        "#{cs_host}/engines/#{engine}/markets/#{market}/securities/#{security}.#{cs_extension}?#{query}"


make_image = (element, engine, market, security, options, width, height) ->
    $('<img>').attr
        src:    make_url(element, engine, market, security, options)
        width:  width
        height: height

    



class ChartWidget
    constructor: (element, @engine, @market, @param, @options = {}) ->
        @state = undefined
        
        @element = $(element)

        return unless @element
        return unless @engine
        return unless @market
        return unless @param

        @state = on
        
        @refresh_timeout = parseInt('' + options['refresh_timeout'])

        @render()
    
    render: =>
        return unless @state is on
        make_image(@element, @engine, @market, @param, @options).bind 'load', @onImageLoad

    refresh: =>
        setTimeout @render, @refresh_timeout
    
    onImageLoad: (event) =>
        @element.html(event.currentTarget)

        @element.css('height', @element.height())

        @element.trigger('render:complete')

        @refresh() if @refresh_timeout
        


chart_widget = (element, engine, market, param, options = {}) ->
    chart = new ChartWidget($(element), engine, market, param, options)


scope.chart = chart_widget
scope.chart_url = make_url